#%Module1.0

proc basic_package_setup { } {
    global ModulesCurrentModulefile

    # Find the package directory from the module file path, the path
    # will be:
    #     /some-root-path/modules/package-name/version-number
    # and we need to change this into:
    #     /some-root-path/packages/package-name/version-number
    set pkg_dir [ string map {/modules/ /packages/} $ModulesCurrentModulefile ]

    set ver [ file tail $ModulesCurrentModulefile ]
    set pkg [ file tail [ file dirname $ModulesCurrentModulefile ] ]

    module-whatis "provides '$pkg' version '$ver'"

    #
    # General setup for all packages.
    #

    if { [ file isdirectory $pkg_dir/bin ] } {
        prepend-path PATH $pkg_dir/bin
    }

    if { [ file isdirectory $pkg_dir/man ] } {
        prepend-path MANPATH $pkg_dir/man
    }

    if { [ file isdirectory $pkg_dir/share/man ] } {
        prepend-path MANPATH $pkg_dir/share/man
    }

    if { [ file isdirectory $pkg_dir/info ] } {
        prepend-path INFOPATH $pkg_dir/info
    }

    if { [ file isdirectory $pkg_dir/share/info ] } {
        prepend-path INFOPATH $pkg_dir/share/info
    }

    if { [ file isdirectory $pkg_dir/lib/pkgconfig ] } {
        prepend-path PKG_CONFIG_PATH $pkg_dir/lib/pkgconfig
    }

    if { [ file isdirectory $pkg_dir/share/pkgconfig ] } {
        prepend-path PKG_CONFIG_PATH $pkg_dir/share/pkgconfig
    }

    if { [ file isdirectory $pkg_dir/include ] } {
        prepend-path CPATH $pkg_dir/include
    }

    if { [ file isdirectory $pkg_dir/lib/python2.7/site-packages ] } {
        prepend-path PYTHONPATH $pkg_dir/lib/python2.7/site-packages
    }

    if { [ file isdirectory $pkg_dir/lib/perl5 ] } {
        prepend-path PERL5LIB $pkg_dir/lib/perl5
    }

    if { [ file isdirectory $pkg_dir/lib64 ] } {
        prepend-path LD_LIBRARY_PATH $pkg_dir/lib64
    }

    #
    # Package specific setup tasks.
    #

    if { $pkg == "automake" } {
       foreach dir [glob -type d $pkg_dir/share/aclocal-*] {
           prepend-path ACLOCAL_PATH $dir
       }
    }
}

proc ModulesHelp { } {
    global ModulesCurrentModulefile
    set ver [ file tail $ModulesCurrentModulefile ]
    set pkg [ file tail [ file dirname $ModulesCurrentModulefile ] ]
    set pkg_dir [ string map {/modules/ /packages/} $ModulesCurrentModulefile ]

    puts stderr "This is version '$ver' of '$pkg', installed into:"
    puts stderr "  $pkg_dir"
}

# Local Variables:
# mode: tcl
# End:
